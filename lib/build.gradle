apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
}

task generateJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs

    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    exclude '**/internal/**'

    options.links 'http://docs.oracle.com/javase/7/docs/api/'

    options.encoding 'UTF-8'

    options.memberLevel = JavadocMemberLevel.PUBLIC

    setTitle 'IBM MobileFirst Platform Android Analytics Specification API'
}

task androidJavadocsJar(type: Jar, dependsOn: generateJavadoc) {
    classifier = 'javadoc'
    from generateJavadoc.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task androidLibraryJar(type: Jar) {
    from 'build/intermediates/classes/release/'
    archiveName project.name + '.jar'
    exclude '**/BuildConfig.*'
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
    archives androidLibraryJar
}

signing {
    required { isReleaseBuild() }
    sign configurations.archives
}

def getRepositoryUsername() {
    return hasProperty('ossrhUsername') ? ossrhUsername : ''
}

def getRepositoryPassword() {
    return hasProperty('ossrhPassword') ? ossrhPassword : ''
}

def getReleaseRepositoryUrl() {
    return 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
}

def getSnapshotRepositoryUrl() {
    return 'https://oss.sonatype.org/content/repositories/snapshots/'
}

def isReleaseBuild() {
    return !getVersion().contains('-SNAPSHOT')
}

def getVersion() {
    return '1.0.0-SNAPSHOT'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: getReleaseRepositoryUrl()) {
                authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
            }

            snapshotRepository(url: getSnapshotRepositoryUrl()) {
                authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
            }

            pom.groupId='com.ibm.mobilefirstplatform.clientsdk.android'
            pom.artifactId='analyticsapi'
            pom.version = getVersion()

            pom.project {
                name 'IBM MobileFirst Platform - Client SDK Android Analytics Specification'
                description 'This is the analytics specification for the IBM MobileFirst Platform Android SDK.'
                url 'https://github.com/ibm-bluemix-mobile-services/mfp-clientsdk-android-analyticsspec'

                scm {
                    connection 'scm:git:git@github.com:ibm-bluemix-mobile-services/mfp-clientsdk-android-analyticsspec.git'
                    developerConnection 'scm:git:git@github.com:ibm-bluemix-mobile-services/mfp-clientsdk-android-analyticsspec.git'
                    url 'https://github.com/ibm-bluemix-mobile-services/mfp-clientsdk-android-analyticsspec'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        name 'IBM Bluemix Services Mobile SDK'
                        email 'mobilsdk@us.ibm.com'
                    }
                }
            }
        }
    }
}
